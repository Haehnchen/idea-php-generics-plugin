import org.gradle.api.tasks.testing.logging.TestExceptionFormat

plugins {
    id "org.jetbrains.intellij" version "0.4.8"
    id 'com.palantir.git-version' version "0.11.0"
}

def htmlFixer = { htmlFile -> file(htmlFile).text.replace('<html>', '').replace('</html>', '') }

apply plugin: 'idea'
apply plugin: 'org.jetbrains.intellij'
apply plugin: 'java'

intellij {
    version ideaVersion
    pluginName 'PHP Generics'
    updateSinceUntilBuild false
    plugins = [
            "com.jetbrains.php:${phpPluginVersion}",
            "org.jetbrains.plugins.phpstorm-remote-interpreter:${phpRemoteInterpreter}",
            "de.espend.idea.php.annotation:${annotationPluginVersion}",
            // "org.jetbrains.plugins.phpstorm-docker:201.6668.60", // for remote interpreter testing
            'CSS',
            'java-i18n',
            'properties',
            'yaml',
            'JavaScriptLanguage',
            "java"
    ]
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

patchPluginXml {
    sinceBuild '201.6668'
    changeNotes = htmlFixer('src/main/resources/META-INF/change-notes.html')
}

publishPlugin {
    token System.getenv('IJ_TOKEN')
}

group 'de.espend.idea.php.generics'

def details = versionDetails()
if (details.isCleanTag) {
    version = "${details.lastTag}"
} else {
    version = "${details.lastTag}.${details.gitHash}-SNAPSHOT"
}

wrapper {
    gradleVersion '6.3'
}

test.testLogging.exceptionFormat = TestExceptionFormat.FULL
